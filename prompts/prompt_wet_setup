setopt promptsubst

autoload -Uz add-zsh-hook
autoload -Uz vcs_info

zstyle ':vcs_info:*' enable git
zstyle ':vcs_info:*' check-for-changes false
zstyle ':vcs_info:*' actionformats \
    '%B%F{cyan}(%b)%f%%b%c'
zstyle ':vcs_info:*' formats \
    '%B%F{cyan}(%b)%f%%b%c'

# we want to catch unstaged and untracked in one swoop
zstyle ':vcs_info:git*+set-message:*' hooks git-dirty
function +vi-git-dirty {
    if [[ $(git rev-parse --is-inside-work-tree 2> /dev/null) == 'true' ]]
    then
        if git status --porcelain | grep -Fq -e 'A' -e '??' -e 'M'
        then
            hook_com[staged]+=' %B%F{yellow}*%f%b '
        else
            hook_com[staged]+=' %B%F{green}%f%b'
        fi
    fi
}

add-zsh-hook -Uz precmd vcs_info
if [ -n "$SSH_CLIENT" ] || [ -n "$SSH_TTY" ]; then
        PS1='[%B%f%b%n@%F{blue}%m%f:%B%(!.%F{red}.%F{blue})%~%f%b] %(?..%F{red}%? %f)${vcs_info_msg_0_}%# '
else
        PS1='[%B%f%b%n:%B%(!.%F{red}.%F{blue})%~%f%b] %(?..%F{red}%? %f)${vcs_info_msg_0_}%# '
fi
# vim: ft=zsh
